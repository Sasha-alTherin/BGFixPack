//creaturefixes.tph -- patches which solely affect creature files
// ----------------------------------------------------------------------------
//Bandit Scalp Pickpocket Exploit Fix
COPY_EXISTING_REGEXP ~.*\.cre~ ~override~
 PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN
  READ_LONG 700 ~itm_loc~
  READ_LONG 704 ~itm_num~
  FOR (y = 0; y < %itm_num%; y = y + 1) BEGIN
   READ_ASCII %itm_loc% ~itm_name~
   PATCH_IF (~MISC86~ STRING_EQUAL_CASE ~%itm_name%~) BEGIN
//    PATCH_PRINT ~%SOURCE_RES% has %itm_name%~
    READ_LONG (%itm_loc% + 16) ~flag~
    PATCH_IF !(%flag% = 2) BEGIN
     WRITE_LONG (%itm_loc% + 16) 2
    END
   END
   SET itm_loc = (%itm_loc% + 20)
  END
 END
BUT_ONLY_IF_IT_CHANGES
//death variables
//shamelessly taken from UB
ACTION_FOR_EACH file IN ~sugar~ ~ivanne~ ~sorrel~ ~ADDY~ ~BRENNA~ ~CAEDMO~ ~ELMIN3~ ~JONAVI~
~KAELLA~ ~KARAN1~ ~PARDA1~ ~SLAVFREE~ ~VOLO~ ~ZOMBIEB~ ~HAFFG2~ ~HALFG2~ ~MTOB4~ ~MTOB5~ BEGIN
 COPY_EXISTING ~%file%.cre~ ~override~
  WRITE_ASCIIE 0x280 ~%SOURCE_RES%~ #18 //assign new
 BUT_ONLY_IF_IT_CHANGES
END //and action_for_each
COPY_EXISTING SKELETB.CRE OVERRIDE // Assign death variable
 WRITE_ASCII 0x280 ~SKELETONB~
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING NOBW4.CRE OVERRIDE  // Assign death variable
 WRITE_ASCII 0x280 ~Noblewoman4~
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING TRAVEL.CRE OVERRIDE // Traveler (Wade), AR2301
 WRITE_ASCII 0x280 WADE #18 // Script name
BUT_ONLY_IF_IT_CHANGES
// ----------------------------------------------------------------------------
// Some creatures carry items that don't exist due to a typo in the filename. Correct files assigned.
// ----------------------------------------------------------------------------
ACTION_FOR_EACH file IN ~dead2.cre~ ~deadfuck.cre~ ~flamang.cre~ ~flampun.cre~ ~flampun2.cre~ ~flamsco.cre~
~flamwiz.cre~ ~jamie.cre~ ~kent.cre~ ~lothan.cre~ ~aasim.cre~ ~amaran.cre~ ~sakul.cre~ ~drelik.cre~
~SKELDED.CRE~ ~CULTASS.CRE~ BEGIN
ACTION_IF (FILE_EXISTS_IN_GAME ~%file%~) THEN BEGIN
 COPY_EXISTING ~%file%~ ~override~
  PATCH_IF (%SOURCE_SIZE% > 0x2d4) BEGIN
   READ_LONG 0x2b8 ~itm_slots~
   READ_LONG 0x2bc ~itm_loc~
   READ_LONG 0x2c0 ~itm_num~
   FOR (x = 0; x < %itm_num%; x = x + 1) BEGIN
    READ_ASCII (0 + (%itm_loc% + (%x% * 0x14))) ~resref~
    PATCH_IF (~SWIH04~ STRING_COMPARE_REGEXP ~%resref%~ = 0)
          OR (~SW1HO4~ STRING_COMPARE_REGEXP ~%resref%~ = 0) BEGIN
     WRITE_ASCII (0 + (%itm_loc% + (%x% * 0x14))) ~SW1H04~
    END
    PATCH_IF (~B00T01~ STRING_COMPARE_REGEXP ~%resref%~ = 0) BEGIN
     WRITE_ASCII (0 + (%itm_loc% + (%x% * 0x14))) ~BOOT01~
    END
    PATCH_IF (~SHIELD04~ STRING_COMPARE_REGEXP ~%resref%~ = 0) BEGIN
     WRITE_ASCII (0 + (%itm_loc% + (%x% * 0x14))) ~SHLD04~ (8)
    END
    PATCH_IF (~MAGE2~ STRING_COMPARE_REGEXP ~%resref%~ = 0) BEGIN
     WRITE_ASCII (0 + (%itm_loc% + (%x% * 0x14))) ~MAGE02~
    END
    PATCH_IF (~SCRL1J~ STRING_COMPARE_REGEXP ~%resref%~ = 0) BEGIN
     PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_REGEXP ~sakul~ = 0) BEGIN
      WRITE_ASCII (0 + (%itm_loc% + (%x% * 0x14))) "SCRL1I" #8        // Hold Person
     END
     PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_REGEXP ~drelik~ = 0) BEGIN
      WRITE_ASCII (0 + (%itm_loc% + (%x% * 0x14))) "SCRL1L" #8 // Monster Summoning I
     END
    END
   END
  END
 BUT_ONLY_IF_IT_CHANGES
END
END //end action for each
// ----------------------------------------------------------------------------
// Change item assignments so correct items are in slots
// ----------------------------------------------------------------------------
//taken from UB
COPY_EXISTING RINNIE.CRE OVERRIDE // Rinnie: LEAT01 (Leather Armor)
              ZHURLO.CRE OVERRIDE // Zhurlong: LEAT01 (Leather Armor)
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  READ_LONG  0x2b8 "slot_off" ELSE 0
  READ_LONG  0x2bc "itm_off" ELSE 0
  READ_LONG  0x2c0 "itm_num" ELSE 0
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    READ_ASCII ("%itm_off%" + (0x14 * "%index%")) "item"
    PATCH_IF ("%item%" STRING_COMPARE_CASE "LEAT01" = 0)
    BEGIN
      FOR (index2 = 0 ; index2 < 36 ; index2 = index2 + 1) BEGIN // search through slots and add reference to first null slot
        READ_SHORT ("%slot_off%" + ("%index2%" + 0x02)) "ref"
        PATCH_IF ("%ref%" = 0x01) BEGIN // if incorrect reference in armor
          WRITE_SHORT ("%slot_off%" + ("%index2%" + 0x02)) "%index%" // adds reference to item
          SET "index2" = 36 // kills loop
        END
        PATCH_IF ("%index2%" = 0) BEGIN // if end of armor slots, skip ahead to inventory
          SET "index2" = 20 // otherwise go to inventory slots
        END
      END
    END
  END
 END
BUT_ONLY_IF_IT_CHANGES
//taken from UB -- correct bad script give them something usefull
COPY_EXISTING BIRD_INE.CRE OVERRIDE
              BIRD_INN.CRE OVERRIDE
              BIRD_INS.CRE OVERRIDE
              BIRD_INW.CRE OVERRIDE
WRITE_ASCII 0x258 ~RANDFLY~ #8
BUT_ONLY_IF_IT_CHANGES
// ----------------------------------------------------------------------------
// Gender corrections -- from UB
// ----------------------------------------------------------------------------
ACTION_FOR_EACH file IN ~IKE.CRE~ ~JONDAL.CRE~ ~ANDRIS.CRE~ ~BARESH2.CRE~ ~BARWLF.CRE~ ~BEARPO3.CRE~ ~BEYN.CRE~ ~CUCHOL.CRE~ ~CULT2.CRE~
~DELSVIR.CRE~ ~DOOMDUR.CRE~ ~DRADEE.CRE~ ~DRADEE2.CRE~ ~GARAN.CRE~ ~KING.CRE~ ~LEAGGU1.CRE~ ~LEAGGU4.CRE~ ~LEAGGU5.CRE~
~LEAGGU6.CRE~ ~LEAGGU7.CRE~ ~MARCEL.CRE~ ~MTOWUB_A.CRE~ ~MTOWUB_B.CRE~ ~MTOWUB_C.CRE~ ~MTOWUB_D.CRE~ ~MTOWUB_E.CRE~
~MTOWUBSC.CRE~ ~MTOWUBSN.CRE~ ~MTOWUBST.CRE~ ~MTOWUBX.CRE~ ~TARNOR.CRE~ ~ULF.CRE~ BEGIN
ACTION_IF (FILE_EXISTS_IN_GAME ~%file%~) THEN BEGIN
COPY_EXISTING ~%file%~ ~OVERRIDE~
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  WRITE_BYTE 0x237 1                  // Sex: Male
  WRITE_BYTE 0x275 1                  // Gender: Male
 END
BUT_ONLY_IF_IT_CHANGES
END
END //end action for each
ACTION_FOR_EACH file IN ~JAHEIR4.CRE~ ~JAHEIR6.CRE~ ~arkush.cre~ ~desret.cre~ ~diana.cre~ ~joia.cre~ ~kirian.cre~
~knight2.cre~ ~knight5.cre~ ~sendai.cre~ ~servwg.cre~ ~serwen.cre~ ~sleepfh.cre~ ~tamah.cre~ ~vayya.cre~ ~voleta.cre~
~wiven.cre~ ~wudei.cre~ ~CLAIRD.CRE~ ~CULTT3.CRE~ ~CULTT4.CRE~ ~DAESE.CRE~ ~DELAIN2.CRE~ ~FTOWUB_A.CRE~
~FTOWUB_B.CRE~ ~FTOWUB_E.CRE~ ~FTOWUBSC.CRE~ ~FTOWUBSN.CRE~ ~FTOWUBX.CRE~ ~ISLSIR.CRE~ ~JAHEIR4.CRE~
~JAHEIR6.CRE~ ~LEAGGU2.CRE~ ~LEAGGU3.CRE~ ~LOVEM.CRE~ ~MARALE2.CRE~ ~PHEOARCH.CRE~ ~TRACEA.CRE~ BEGIN
ACTION_IF (FILE_EXISTS_IN_GAME ~%file%~) THEN BEGIN
COPY_EXISTING ~%file%~ ~OVERRIDE~
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
 WRITE_BYTE 0x237 2                  // Sex: Female
 WRITE_BYTE 0x275 2                  // Gender: Female
 END
BUT_ONLY_IF_IT_CHANGES
END
END //end action for each
ACTION_FOR_EACH file IN ~APPAR.CRE~ ~CRYPTCRA.CRE~ ~GHASTD.CRE~ ~GRAEL.CRE~ ~HACK.CRE~ ~PLYOGRE.CRE~ ~SKELET.CRE~ ~SPIDPHAS.CRE~ ~STALKE.CRE~ BEGIN
ACTION_IF (FILE_EXISTS_IN_GAME ~%file%~) THEN BEGIN
COPY_EXISTING ~%file%~ ~OVERRIDE~
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  WRITE_BYTE 0x237 1                  // Sex: Male
  WRITE_BYTE 0x275 4                  // Gender: Neither
 END
BUT_ONLY_IF_IT_CHANGES
END
END //end action for each

//FARMBR, GERDE, JEBADO, SONNER, TELMEN: Removed SHOUT.BCS to prevent spawned BANDIT.CRE from turning Brun, Gerde, Jebadoh, Sonner, and Telmen, hostile
COPY_EXISTING ~FARMBR.CRE~ ~override~
       ~GERDE.CRE~  ~override~
  WRITE_ASCII 0x248 "" #8 //override script
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~JEBADO.CRE~ ~override~
       ~SONNER.CRE~ ~override~
       ~TELMEN.CRE~ ~override~
  WRITE_ASCII 0x250 "" #8 //class script
BUT_ONLY_IF_IT_CHANGES
//FLAM: Alora calls a neutral allegiance, inanimate Flaming Fist Enforcer fix
COPY_EXISTING ~FLAM.CRE~ ~override~
  WRITE_BYTE 0x270 0xff
BUT_ONLY_IF_IT_CHANGES
//SKELETS: Randomly spawning skeletons now have their correct immunities
COPY_EXISTING ~SKELETS.CRE~ ~override~
  ADD_CRE_ITEM ~RING99~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~LRING~
BUT_ONLY_IF_IT_CHANGES
//ZOMBIE, ZOMBIE_A, ZOMBIEB: Corrected damage and dying sounds that were skeleton sounds
COPY_EXISTING ~ZOMBIE.CRE~ ~override~
              ~ZOMBIE_A.CRE~ ~override~
              ~ZOMBIEB.CRE~ ~override~
  SAY 0xec #18085 //damage sound
  SAY 0xf0 #18086 //dying sound
BUT_ONLY_IF_IT_CHANGES


//totsc only stuff
ACTION_IF (GAME_IS ~totsc~) BEGIN
// ----------------------------------------------------------------------------
//Creature corrections for ToTSC only
// ----------------------------------------------------------------------------
//creatures need scripting variables
COPY_EXISTING ~cultt1.cre~ ~override~
 WRITE_ASCIIT 0x280 ~Cult_Wizard~ //assign new
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~cultt4.cre~ ~override~
 WRITE_ASCIIT 0x280 ~Cult_Archer~ //assign new
BUT_ONLY_IF_IT_CHANGES
//shamelessly taken from UB
ACTION_FOR_EACH file IN ~FEARM~ ~PRIDEM~ BEGIN
 COPY_EXISTING ~%file%.cre~ ~override~
  WRITE_ASCIIE 0x280 ~%SOURCE_RES%~ #18 //assign new
 BUT_ONLY_IF_IT_CHANGES
END //and action_for_each
//LEAGGU4: Some Merchant League Guards no longer have an old woman’s greeting
 COPY_EXISTING ~LEAGGU4.CRE~ ~override~
  SAY 0xa4 #4964
  SAY 0x10c #4964
  SAY 0xec #12566
  SAY 0x0f0 #12567
 BUT_ONLY_IF_IT_CHANGES
//ToTSC error that Farmers got mistakenly named Lahl
 COPY_EXISTING ~FARM.CRE~ ~override~
//       ~FARM2.CRE~ ~override~
       ~FARM3.CRE~ ~override~
       ~FARM4.CRE~ ~override~
//       ~FARMBE.CRE~ ~override~
       ~FARMER.CRE~ ~override~
//       ~FARMWE.CRE~ ~override~
  SAY 0x8 @6
  SAY 0xc @6
 BUT_ONLY_IF_IT_CHANGES
// ----------------------------------------------------------------------------
// Change item assignments so correct items are in slots
// ----------------------------------------------------------------------------
COPY_EXISTING ANDRIS.CRE OVERRIDE   // Andris: CLCK12 (Knave's Robe)
              MARCEL.CRE OVERRIDE   // Marcellus: CLCK13 (Traveller's Robe)
              SHANDAL2.CRE OVERRIDE // Shandalar: CLCK16 (Cloak of the Neutral Archmagi)
 PATCH_IF (SOURCE_SIZE > 0x2d4) BEGIN
  READ_LONG  0x2b8 "slot_off" ELSE 0
  READ_LONG  0x2bc "itm_off" ELSE 0
  READ_LONG  0x2c0 "itm_num" ELSE 0
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    READ_ASCII ("%itm_off%" + (0x14 * "%index%")) "item"
    PATCH_IF ("%item%" STRING_COMPARE_CASE "clck12" = 0) OR
             ("%item%" STRING_COMPARE_CASE "clck13" = 0) OR
             ("%item%" STRING_COMPARE_CASE "clck16" = 0) BEGIN
      WRITE_SHORT ("%slot_off%" + 0x02) "%index%" // puts in armor slot
      WRITE_SHORT ("%slot_off%" + 0x22) 0xffff    // removes entry from cloak slot
    END
  END
 END
BUT_ONLY_IF_IT_CHANGES

END